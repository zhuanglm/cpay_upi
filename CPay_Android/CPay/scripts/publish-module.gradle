apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'



task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}


task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}
//
//artifacts {
//    archives androidSourcesJar
//    archives javadocJar
//}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidJavadocsJar
                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Citcon mobile official Android SDK'
                    url = 'https://github.com/Citcon/citcon_mobile_sdk_android'
                    licenses {
                        license {
                            name = 'The MIT License'
                        }
                    }
                    developers {
                        developer {
                            id = 'raymond.zhuang'
                            name = 'Raymond Zhuang'
                            email = 'raymond.zhuang@citcon-inc.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/Citcon/citcon_mobile_sdk_android.git'
                        developerConnection = 'scm:git:ssh://github.com/Citcon/citcon_mobile_sdk_android.git'
                        url = 'https://github.com/Citcon/citcon_mobile_sdk_android'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            //  rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
